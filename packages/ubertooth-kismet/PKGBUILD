# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ubertooth-kismet'
pkgver='2017.03.R2'
_kismet_pkgver=2013-03-R1b
pkgrel=1
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc='A kismet plugin for ubertooth.'
url='http://sourceforge.net/projects/ubertooth/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('kismet' 'ubertooth')
source=("https://github.com/greatscottgadgets/ubertooth/archive/${pkgver//./-}.tar.gz"
        "http://www.kismetwireless.net/code/kismet-${_kismet_pkgver}.tar.gz")
sha1sums=('a685db6317ee7e125da0a5ea93b694acc41a5cc6'
          'c77b92bf5d589b79281271e96724ef96032a162b')

build() {
  cd "$srcdir/kismet-$_kismet_pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var

  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  make KIS_SRC_DIR="$srcdir/kismet-$_kismet_pkgver"
}

package() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  mkdir -p "$pkgdir/usr/lib"

  make DESTDIR="$pkgdir/" LIBDIR=/usr/lib install
}
